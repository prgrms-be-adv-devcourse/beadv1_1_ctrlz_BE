import org.gradle.api.tasks.compile.JavaCompile

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
}

ext {
    springCloudVersion = "2025.0.0"
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'common'

def queryDslVersion = "7.1" // 25년 11월 14일 기준 가장 최신
def querydslSrcDir  = layout.buildDirectory.dir("generated/querydsl").get().asFile // build/generated/querydsl

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.7"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

repositories {
    mavenCentral()
}

dependencies {


    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // https://mvnrepository.com/artifact/com.github.f4b6a3/uuid-creator
    // UUID creator (UUIDv7 등 생성 가능)
    implementation 'com.github.f4b6a3:uuid-creator:6.1.1'

    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.github.codemonstur:embedded-redis:1.4.3'

    //openfeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign.form:feign-form-spring:3.8.0'

    //Querydsl
    implementation("io.github.openfeign.querydsl:querydsl-core:${queryDslVersion}")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa"
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL 설정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

sourceSets {
    main {
        java {
            srcDirs += querydslSrcDir
        }
    }
}
configurations {
    querydsl.extendsFrom compileClasspath
}